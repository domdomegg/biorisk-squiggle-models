// More detailed derivations for some of the variables we provide in the paper and use in the model

// BSL-3 are probably relevant
// Jocelyn Kaiser. "Taking Stock of the Biodefense Boom"
// https://www.science.org/doi/10.1126/science.333.6047.1214
// 1495 labs were registered with the CDC in 2011
//
// Filippa Lentzos et al. "Global BioLabs Report 2023"
// https://www.kcl.ac.uk/warstudies/assets/global-biolabs-report-2023.pdf
// Approximately 1/3 of BSL-3+ labs are in the US
// There's also been about a 2x growth in BSL4 labs since 2011, when the 1495 figure is from. Possibly the same in BSL3 (1.5 to 2)
// 
// There is also likely underreporting to the CDC, as there are unregistered labs. A factor of 1 to 1.2 is used to account for this.
n = 1495 * 3 * (1.5 to 2) * (1 to 1.2)

// Ron A. M. Fouchier. "Studies on Influenza Virus Transmission between Ferrets: the Public Health Risks Revisited"
// https://journals.asm.org/doi/10.1128/mbio.02560-14
// 4 of 292 BSL-3 labs in the US reported leaks in 7 years, giving: ~0.2% per year
//
// Lynn C. Klotz. "Sharpening Our Intuition on Man-made Pandemics"
// https://web.archive.org/web/20120831234930/http://bio-security.org/wp-content/uploads/2012/05/SharpeningOurIntuition0515.pdf
// 312 lab years of PPP research, 3 leaks: ~1%
// <897 lab years at BSL4, 3 leaks: ~0.33%
// (also references NRC report below)
//
// National Research Council. "Evaluation of a Site-Specific Risk Assessment for the Department of Homeland Security's Planned National Bio- and Agro-Defense Facility in Manhattan, Kansas"
// https://nap.nationalacademies.org/catalog/13031/evaluation-of-a-site-specific-risk-assessment-for-the-department-of-homeland-securitys-planned-national-bio-and-agro-defense-facility-in-manhattan-kansas
// The Site-Specific Risk Assessment "estimates indicate that the probability of an infection resulting from a laboratory release of FMDv from the NBAF in Manhattan, Kansas approaches 70% over 50 years".
// Annualised this is (1-(1-0.7)^(1/50)) = 2.4% 
//
// These also might be underestimates because the US has higher biosafety standards than many other countries, this looks at regulated labs, and there may be incentives to underreport leaks.
// We used: 0.2% to 2%
// Given this is an uncertainty about a probability, we then fit this to a beta distribution using https://nunosempere.com/blog/2023/03/15/fit-beta/
l = beta(2.42, 267) * bioscienceLeaksRatio

// Ron A. M. Fouchier. "Studies on Influenza Virus Transmission between Ferrets: the Public Health Risks Revisited"
// https://journals.asm.org/doi/10.1128/mbio.02560-14
// "Previous studies used 5 to 60% as the probability of onward transmission, which is based on the unlimited spread of a pandemic influenza virus in the general population". However, "scientifically justified reduction [...] can be made, based on the above-mentioned biosafety measures and risk mitigation strategies that are in place"
// - Vaccination and treatment could reduce onwards transmission. Fouchier suggests this is by >65% for influenza, however this assumes the best vaccine type and refers to vaccine efficacy and not onwards transmission data. Other sources suggest 40-60% for other pathogens. For example, for COVID it seems to be ~40%: https://www.ncbi.nlm.nih.gov/pmc/articles/PMC8569927/
// - Quarantine measures. We believe this paper overestimates this by assuming that all people immediately know when they're infected and quarantine perfectly. We think people will likely be better than the general public at recognising symptoms and quarantining, but it's unclear by how much. We have approximated by this as being twice as good as the average person's measures to prevent onward transmission.
// 
// Lynn C. Klotz and Edward J. Sylvester. "The Consequences of a Lab Escape of a Potential Pandemic Pathogen"
// https://www.frontiersin.org/journals/public-health/articles/10.3389/fpubh.2014.00116/full
// 1-30% probability to seed a pandemic
// 
// Stefano Merler, Marco Ajelli, Laura Fumanelli and Alessandro Vespignani. "Containing the accidental laboratory escape of potential pandemic influenza viruses"
// https://link.springer.com/article/10.1186/1741-7015-11-252
// 25-80% probability to trigger an outbreak, assuming no interventions
// 
// We used: (5% to 60%) * (40% to 60%) * (50%) â‰ˆ 2.5% to 15%
// Given this is an uncertainty about a small probability, we then fit this to a beta distribution using https://nunosempere.com/blog/2023/03/15/fit-beta/
i = beta(3.54, 42.4)

// United Nations, Department of Economic and Social Affairs. "2024 Revision of World Population Prospects"
// https://population.un.org/wpp/
// Total population estimate, as of 1 July (IndicatorId = 49)
// Mean taken from 1970 to 2020
avgWorldPopulation1970to2020 = 5752441470
// National Consortium for the Study of Terrorism and Responses to Terrorism. "The Global Terrorism Database"
// https://www.start.umd.edu/gtd/
// By successful, we mean attacks with >10 fatalities.
// Between 1970 and 2020 (dates for which data is available), there were attacks with:
//   11-50 fatalities: 427
//   51-100 fatalities: 30
//   100+ fatalities: 11
successfulTerroristAttacks1970to2020 = 427 + 30 + 11
t = increaseInRadicalisationRatio * successfulTerroristAttacks1970to2020 / avgWorldPopulation1970to2020 / (2020 - 1970)

// OECD. "OECD Indicators: Education at a Glance"
// https://www.oecd-ilibrary.org/education/education-at-a-glance-2023_e13bef63-en
// 543k people graduated with a Biology PhD in OECD countries from 2013 to 2021 (9 years).
//
// We assume the working population for PhD graduates is those aged 30 to 65. (Average PhD graduates at roughly age 30, average retirement age of developed economies is 65).
//
// This estimates the total number of biology PhD grads, assuming they exist at the same rate in the working population are currently graduating. This is likely an overestimate, although we're underestimating the total PhDs by only considering OECD countries above.
o = 543k * (65 - 30) / 9
